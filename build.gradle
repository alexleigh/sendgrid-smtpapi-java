/**
 * Commands:
 *   - gradle build
 *   - gradle test
 *   - gradle assemble
 *   - gradle uploadArchives
 *
 * To execute the 'uploadArchives' task, the following properties must be specified
 * in an external 'gradle.properties' file:
 *   - sonatypeUsername
 *   - sonatypePassword
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

group = 'com.revinate'
version = '1.2.1'
ext.packaging = 'jar'

if (!hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = null
    ext.sonatypePassword = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

dependencies {
    compile 'org.json:json:20140107'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

repositories {
    mavenCentral()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                artifactId 'sendgrid-smtpapi-java'
                packaging 'jar'
                name 'SendGrid SMTPAPI for Java'
                description 'SendGrid SMTPAPI Java helper library'
                url 'https://github.com/revinate/sendgrid-smtpapi-java'

                scm {
                    url 'https://github.com/revinate/sendgrid-smtpapi-java'
                    connection 'scm:git:git://github.com/revinate/sendgrid-smtpapi-java.git'
                    developerConnection 'scm:git:ssh://git@github.com:revinate/sendgrid-smtpapi-java.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'revinate'
                        name 'Revinate, Inc'
                    }
                }
            }
        }
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
